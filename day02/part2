#! /bin/zsh

. ${0:P:h:h}/utils

is_safe() {
  print -rl -- $@ |
    awk '
      NR == 1 { last = $1 }
      NR > 1 {
        diff=$1 - last
        if (!diff) {
          exiting=1; exit
        }
        sign = diff > 0
        if ((NR > 2 && streak != sign) || diff < -3 || diff > 3) {
          exiting=1; exit
        }
        streak = sign
        last = $1
      }
      END { if (!exiting) { print "y" } }
    '
}

is_safe_dampened() {
  # i: loop variable. If not local, breaks zargs :/
  # $@ is a single string, the line of the input
  local i args=($=@)
  for i ({1..$#args});
    is_safe $args[1,i-1] $args[i+1,-1] | grep y && return
}

zargs -P$(nproc) -n1 -- "${(f)$(cat $1)}" -- is_safe_dampened | wc -l
